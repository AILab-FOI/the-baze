' Gambas class file

' json TextEditor
Public editor As TextEditor
' type of query
Public query_type As String
' unique key
Public unique_key As Integer
' name of the attribute that contains object subtype (like ObjectId, Timestamp, BinData, ...)
Private bsonTypeIdentifier As String

Public Sub _new()
  ' make TextEditor visible
  editor = TextEditorJSON
  VSplitMain.Layout = [1, 3]
  TextEditorJSON.ShowLineNumber = True
End


Public Sub ToolButtonExecute_Click()
  ' if query then execute it and show the results
  ' else (JSON TextEditor) update the document on the given path
  Dim res, obj As Variant
  bsonTypeIdentifier = AUX.generate_random_closing_identifier(65, 90)
  Try res = FMain.db.new_query(Left$(FMain.main_tabstrip.Text, - Len(" - Find query")), TextEditorJSON.Text, bsonTypeIdentifier)
  If Error Then
    Message.Error(Error.Text)
    Return
  Endif
  TextEditorResult.Text = res["json"]
  TreeViewResult.Clear()
  
  For Each obj In res["collection"]
    create_json_treeview(obj, Null)
  Next
End

Public Sub create_json_treeview(object As Collection, parent As String, Optional isObject As Boolean = True)  ' object can be Collection (in case of objects) or Variant[] (in case of arrays)
  ' construct the json treeview out of the result collection
  Dim attr, elem As Variant
  Dim tmpCollection As Collection
  Dim i As Integer
  Dim ukey, pkey, skey As String
  Dim isAttrObject As Boolean
  Dim bsonTypeName As String
  With TreeViewResult
    If isObject Then
      pkey = Str(get_unique_key())
      If parent Then
        bsonTypeName = Object[bsonTypeIdentifier]
        If bsonTypeName Then
          .Add(pkey, "Object [type=" & bsonTypeName & "]", Picture.Load("images/directory.png"), parent)
          If bsonTypeName <> "null" And If bsonTypeName <> "undefined" Then
            .Add(Str(get_unique_key()), Object["value"], Picture.Load("images/class.png"), pkey)
          Endif
          Return
        Else
          .Add(pkey, "Object", Picture.Load("images/directory.png"), parent)
        Endif
      Else  ' top-level objects (i.e. resulted objects) almost always have "_id" property, so it is easier to find/spot wanted/relevant objects
        Try .Add(pkey, "Object[_id=" & RegExp.Replace(RegExp.Replace(JSON.Encode(Object["_id"]), "\\{\\s*\"" & bsonTypeIdentifier & "\"\\s*:\\s*\"(.*?)\"\\s*,\\s*\"value\"\\s*:\\s*\"((?:[^\\\\\"]|\\\\.)*?)\"\\}", "&1(\"&2\")") & "]", "\\{\\s*\"" & bsonTypeIdentifier & "\"\\s*:\\s*\"(.*?)\"\\s*}", "&1"), Picture.Load("images/directory.png"), parent) ' convert JSON back to the BSON representation, e.g. JSON string like    {"_id":{"BSON_TYPE_IDENTIFIER":"ObjectId", "value":"2343242"}, "attr1":"..."}   will be converted to    {"_id":ObjectId(2343242), "attr1":"..."}
        If Error Then   ' MongoDB database/collection can use mode where "_id" property isn't obligatory - in that case, resulted objects won't have identifiers
          .Add(pkey, "Object", Picture.Load("images/directory.png"), parent)
        Endif
      Endif
    Else
      pkey = parent
    Endif
    
    For Each attr In object
      isAttrObject = True
      skey = Str(get_unique_key())
      If TypeOf(attr) <> gb.Object Then
        If isObject Then
          .Add(skey, object.Key, Picture.Load("images/directory.png"), pkey)
        Else
          skey = pkey
        Endif
      Else If attr Is Array Then
        isAttrObject = False
        If isObject Then
          .Add(skey, Object.Key, Picture.Load("images/directory.png"), pkey)
        Else
          .Add(skey, "Array", Picture.Load("images/directory.png"), pkey) ' in case that array is contained directly in another array, then it would be unnamed so here as second parameter can be set its placeholder
        Endif
        tmpCollection = New Collection
        i = 0
        For Each elem In attr
          tmpCollection[i] = elem
          i += 1
        Next
        attr = tmpCollection
      Else
        If isObject Then
          .Add(skey, object.Key, Picture.Load("images/directory.png"), pkey)
        Else
          skey = pkey
        Endif
      Endif
      If TypeOf(attr) <> gb.Object Then
        ukey = Str(get_unique_key())
        .Add(ukey, attr, Picture.Load("images/class.png"), skey)
      Else
        '.Add(skey, attr, Picture.Load("images/directory.png"), pkey)
        create_json_treeview(attr, skey, isAttrObject)
      Endif
    Next
  End With
End

Public Sub get_unique_key() As Integer
  ' get a unique key
  If Not unique_key Then
    unique_key = 0
  Endif
  unique_key += 1
  Return unique_key
End


Public Sub ToolButtonClose_Click()
  ' close the current tab
  Dim child As Variant
  Dim qname As String
  Dim ok As Boolean = False
  qname = Left$(FMain.main_tabstrip.Text, - Len(" - Find query"))
  If FMain.db.dbobjects["queries"]["find queries"][qname] = TextEditorJSON.Text Then
    If query_type = "find queries" Then
      ok = True
    Endif
  Endif
  If Not ok Then
    ok = Message.Question("Are you sure that you want to abandon all changes?", "Yes", "No") = 1 
  Endif
  If ok Then
    With FMain.main_tabstrip.Current
      For Each child In .Children
        child.Close
      Next
      .Delete
    End With
  Endif
End

Public Sub TextEditorJSON_KeyPress_()
  ' handle copy, paste, cut, select all
  If Key.Code = Key["C"] And If Key.Control Then
    TextEditorJSON.Copy()
  Else
    If Key.Code = Key["V"] And If Key.Control Then
      TextEditorJSON.Paste()
    Else
      If Key.Code = Key["X"] And If Key.Control Then
        TextEditorJSON.Cut()
      Else
        If Key.Code = Key["A"] And If Key.Control Then
          TextEditorJSON.SelectAll()
        Endif
      Endif
    Endif
  Endif
End
