' Gambas class file

Public field_list As String[]
Public caller As Object

Public Sub _new()
  ' initialize the field_list
  field_list = ComboBoxField.List
End

Public Sub reload()
  ' reload constraint if available
  Dim row As Integer
  row = caller.main_table_view.Row
  With caller.pgtable
    If .options.Count < row Then
      Return
    Else
      If IsNull(.options[row]["1d array"]) Then
        .options[row]["1d array"] = False
      Endif
      CheckBoxIsArray.value = .options[row]["1d array"]
      If IsNull(.options[row]["2d array"]) Then
        .options[row]["2d array"] = False
      Endif
      CheckBoxIs2DArray.Value = .options[row]["2d array"]
      If IsNull(.options[row]["precision"]) Then
        .options[row]["precision"] = False
      Else
        CheckBoxPrecision.Value = True
      Endif
      ValueBoxPrecision.Text = .options[row]["precision"]
      If IsNull(.options[row]["field"]) Then
        .options[row]["field"] = False
      Else
        CheckBoxField.Value = True
      Endif
      ComboBoxField.Text = .options[row]["field"]
    Endif
  End With
  Catch
    Return
  
End


Public Sub CheckBoxIsArray_Click()
  ' an attribute is either a 1D array or 2D array, but not both 
  ' so set the 2D array checkbox to false
  If CheckBoxIsArray.Value = True Then
    CheckBoxIs2DArray.Value = False
    CheckBoxIsArray.Value = True
  Endif
  caller.pgtable.options[caller.main_table_view.Row]["1d array"] = CheckBoxIsArray.Value
End

Public Sub CheckBoxIs2DArray_Click()
  ' an attribute is either a 1D array or 2D array, but not both 
  ' so set the 1D array checkbox to false
  If CheckBoxIs2DArray.Value = True Then
    CheckBoxIs2DArray.Value = True
    CheckBoxIsArray.Value = False
  Endif
  caller.pgtable.options[caller.main_table_view.Row]["2d array"] = CheckBoxIs2DArray.Value
End

Public Sub ValueBoxPrecision_KeyRelease()
  ' set the precision for the current attribute
  caller.pgtable.options[caller.main_table_view.Row]["precision"] = ValueBoxPrecision.Text
End



Public Sub CheckBoxPrecision_Click()
  ' enable precision value box
  LabelPrecision.Enabled = CheckBoxPrecision.Value
  ValueBoxPrecision.Enabled = CheckBoxPrecision.Value
End

Public Sub CheckBoxField_Click()
  ' enable field combobox
  LabelField.Enabled = CheckBoxField.Value
  ComboBoxField.Enabled = CheckBoxField.Value
  ' if precision is specified, the field has to include seconds
  If ValueBoxPrecision.Value <> 0 And CheckBoxPrecision.Value Then
    ComboBoxField.List = ["SECOND", "DAY TO SECOND", "HOUR TO SECOND", "MINUTE TO SECOND"]
  Else
    ComboBoxField.List = field_list
  Endif
  ' change the field of the current attribute
  caller.pgtable.options[caller.main_table_view.Row]["field"] = ComboBoxField.Text
End

