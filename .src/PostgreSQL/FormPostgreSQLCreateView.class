' Gambas class file

Public Sub Form_open()
  ' initialize the vsplit
  Dim query As String
  VSplitMain.Settings = [1, 1]
  TextEditorViewQuery.ShowLineNumber = True
  For Each query In FMain.db.dbobjects["queries"]["sql queries"]
    ComboBoxChooseQuery.Add(FMain.db.dbobjects["queries"]["sql queries"].key)
  Next
End


Public Sub RadioButtonCreateFromQuery_Click()
  ' enable the combobox
  ComboBoxChooseQuery.Enabled = True
  TextEditorViewQuery.Enabled = False
End

Public Sub RadioButtonEnterQuery_Click()
  ' enable the TextEditor
  TextEditorViewQuery.Enabled = True
  ComboBoxChooseQuery.Enabled = False
End

Public Sub ToolButtonCancel_Click()
 ' ask the user if he/she is sure and then close the tab
  Dim child As Variant
  If Message.Question("Do you want to discard your view design? If yes all entered data will be lost.", "Yes", "No, continue editing") = 1 Then
    With FMain.main_tabstrip.Current
      For Each child In .Children
        child.Close
      Next
      .Delete
    End With
    FMain.main_tabstrip.index = 0
  Endif
End

Public Sub ComboBoxChooseQuery_Click()
  ' put the query text into the TextEditor
  TextEditorViewQuery.Text = FMain.db.dbobjects["queries"]["sql queries"][ComboBoxChooseQuery.Text]
  TextEditorViewQuery.HighlightAll()
End

Public Sub ToolButtonExecute_Click()
  ' embed the results form in the panel and show the results
  Dim res As FormPostgreSQLQueryResult
  Dim con As New Connection
  con.Type = FMain.db.con.Type
  con.Host = FMain.db.con.Host
  con.Port = FMain.db.con.Port
  con.Login = FMain.db.con.Login
  con.Password = FMain.db.con.Password
  con.Name = FMain.db.con.Name
  con.Open
  res = New FormPostgreSQLQueryResult(PanelResult)
  res.DataSourceMain.Table = TextEditorViewQuery.Text
  res.DataSourceMain.Connection = con
End

Public Sub ToolButtonCreateView_Click()
  ' create the view and close the tab
  Dim child As Object
  Dim repl As String
  If Message.Question("Do you want to create the view?", "Yes", "No, continue editing") = 1 Then
    With FMain.main_tabstrip.Current
      If CheckBoxReplace.Value Then
        repl = " OR REPLACE "
      Else
        repl = " "
      Endif
      If IsNull(FMain.db.execute("CREATE" & repl & "VIEW " & Left$(.Text, -7) & " AS " & TextEditorViewQuery.Text)) Then
        Return
      Else
        With FMain.main_tabstrip.Current
          For Each child In .Children
            child.Close
          Next
            .Delete
        End With
        FMain.db.refresh_db_tree()
        FMain.main_tabstrip.index = 0
        Message.Info("View successfully created!")
      Endif
    End With
    FMain.main_tabstrip.index = 0
  Endif

End

Public Sub TextEditorViewQuery_KeyPress_()
  ' handle copy, paste, cut, select all
  If Key.Code = Key["C"] And If Key.Control Then
    TextEditorViewQuery.Copy()
  Else
    If Key.Code = Key["V"] And If Key.Control Then
      TextEditorViewQuery.Paste()
    Else
      If Key.Code = Key["X"] And If Key.Control Then
        TextEditorViewQuery.Cut()
      Else
        If Key.Code = Key["A"] And If Key.Control Then
          TextEditorViewQuery.SelectAll()
        Endif
      Endif
    Endif
  Endif
End
