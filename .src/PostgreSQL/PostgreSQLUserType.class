' Gambas class file

Public type_name As String
Public attributes As New String[]
Public datatypes As New String[]
Public options As New Collection[]


Public Sub add_attribute(name As String, datatype As String)
  ' create a new attribute
  Dim o As New Collection
  attributes.Add(name)
  datatypes.Add(datatype)
  options.Add(o)
End

Public Sub change_attribute(a_index As Integer, new_name As String, new_datatype As String)
  ' change the attribute under the given index
  If a_index > attributes.Count - 1 Then
    add_attribute(new_name, new_datatype)
  Else
    attributes[a_index] = new_name
    datatypes[a_index] = new_datatype
  Endif
End


Public Sub change_options(attribute As Integer, att_options As Collection)
  options[attribute] = att_options
End


Public Sub toSQL() As String
  ' construct a CREATE TYPE statement
  Dim i As Integer
  Dim statement As String
  statement = "CREATE TYPE \"" & escape(type_name) & "\" AS (" 
  For i = 0 To attributes.Count - 1 
    If attributes[i] = "" Then
      Break
    Endif
    statement &= "\"" & escape(attributes[i]) & "\" "
    statement &= Upper(datatypes[i])
    If options[i]["size"] <> "" Then
      statement &= "(" & options[i]["size"] & ")"
    Endif
    If options[i]["precision"] <> "" Then
      If options[i]["scale"] <> "" Then
        statement &= "(" & options[i]["precision"] & ", " & options[i]["scale"] & ")"
      Else
        If options[i]["fields"] <> "" Then
          statement &= " " & options[i]["fields"] & " (" & options[i]["precision"] & ")" 
        Else
          If datatypes[i] = "time with time zone" Then
            statement = Left(statement, Len(statement) - Len(datatypes[i]))
            statement &= Upper("time (" & options[i]["precision"] & ") with time zone")
          Else
            If datatypes[i] = "time without time zone" Then
              statement = Left(statement, Len(statement) - Len(datatypes[i]))
              statement &= Upper("time (" & options[i]["precision"] & ") without time zone")
            Else
              If datatypes[i] = "timestamp with time zone" Then
                statement = Left(statement, Len(statement) - Len(datatypes[i]))
                statement &= Upper("timestamp (" & options[i]["precision"] & ") with time zone")
              Else 
                If datatypes[i] = "timestamp without time zone" Then
                  statement = Left(statement, Len(statement) - Len(datatypes[i]))
                  statement &= Upper("timestamp (" & options[i]["precision"] & ") without time zone")
                Else
                  statement &= "(" & options[i]["precision"] & ")"
                Endif
              Endif
            Endif
          Endif
        Endif
      Endif
    Endif
    If options[i]["1d array"] <> "" Then
      statement &= "[]"
    Endif
    If options[i]["2d array"] <> "" Then
      statement &= "[][]"
    Endif
    statement &= " "
    statement &= ", "
  Next
  statement = Left(statement, Len(statement) - 2)
  statement &= ")"
  Return statement
End

Public Sub escape(repl_string As String) As String
  ' escape "-characters in a string
  Return Replace(repl_string, "\"", "\"\"")  
End


