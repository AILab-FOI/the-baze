' Gambas class file

Public sql_editor As Editor

Public Sub _new()
  ' make editor visible
  sql_editor = EditorSQL
End


Public Sub Form_Open()
  VSplitMain.Settings = [1, 1]
  EditorSQL.Flags[EditorSQL.ShowLineNumbers] = True
End

Public Sub ToolButtonExecute_Click()
' execute the query and show the results
  Dim results As Object[] 
  Dim one_line As Collection
  Dim value As Variant
  Dim first As Boolean
  Dim counter_row As Integer
  Dim counter_col As Integer
  Dim query_type As String
  Dim length As Integer
  Try results = FMain.db.execute(EditorSQL.Text)
  If Error Then 
    Message.Error(Error.Text)
    Stop Event
    Return
  Else
    Print Right$(FMain.main_tabstrip.Text, 12)
    Select Right$(FMain.main_tabstrip.Text, 12)
      Case "Insert query"
        query_type = "insert queries"
        length = -15
      Case "Delete query"
        query_type = "delete queries"
        length = -15
      Case "Update query"
        query_type = "update queries"
        length = -15
      Default
        query_type = "sql queries"
        length = -12
    End Select
    FMain.db.new_sql_query(Left(FMain.main_tabstrip.Text, length), EditorSQL.Text, query_type) 
    If results Then 
      first = True
      counter_row = 0
      TableViewResults.Rows.Count = results.Count
      If results.Count > 0 Then
        TableViewResults.Columns.Count = results[0].Count
        For Each one_line In results
          counter_col = 0
          For Each value In one_line
            If first Then
              TableViewResults.Columns[counter_col].Title = one_line.Key
            Endif
            TableViewResults.Row = counter_row
            TableViewResults.Column = counter_col
            TableViewResults.Current.Text = value 
            counter_col += 1
          Next
          counter_row += 1
          first = False
        Next
        TableViewResults.Enabled = True
        TableViewResults.Row = 0 
        TableViewResults.Column = 0
      Else
        TableViewResults.Columns.Count = 0
        Message.Info("Query successful, but no rows returned!")
      Endif
    Endif
  Endif
End


Public Sub ToolButtonClose_Click()
  ' close the current tab
  Dim child As Variant
  With FMain.main_tabstrip.Current
    For Each child In .Children
      child.Close
    Next
    .Delete
  End With
End

Public Sub EditorSQL_KeyPress_()
  ' handle copy, paste, cut, select all
  If Key.Code = Key["C"] And If Key.Control Then
    EditorSQL.Copy()
  Else
    If Key.Code = Key["V"] And If Key.Control Then
      EditorSQL.Paste()
    Else
      If Key.Code = Key["X"] And If Key.Control Then
        EditorSQL.Cut()
      Else
        If Key.Code = Key["A"] And If Key.Control Then
          EditorSQL.SelectAll()
        Endif
      Endif
    Endif
  Endif
End
